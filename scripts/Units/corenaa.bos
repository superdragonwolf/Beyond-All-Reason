#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  flare1, flare2, mount, gunbase, guna, gunb, base;

static-var  restore_delay, gun_1;
static-var  Stunned;

// Signal definitions
#define SIG_AIM				2

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn base to x-axis rockAng speed rockSpeed*5;
			turn base to z-axis rockAng speed rockSpeed*5;
			wait-for-turn base around x-axis;
			wait-for-turn base around z-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}


RockUnit(anglex, anglez)
{
	turn base to x-axis anglex speed <15.000000>;
	turn base to z-axis <0> - anglez speed <15.000000>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to x-axis <0.000000> - anglex speed <12.000000>;
	turn base to z-axis anglez speed <12.000000>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to x-axis anglex / <0.005495> speed <9.000000>;
	turn base to z-axis <0> - (anglez / <0.005495>) speed <9.000000>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to x-axis <0.000000> speed <3.000000>;
	turn base to z-axis <0.000000> speed <3.000000>;
}

#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 200

#include "unit_hitbyweaponid_and_smoke.h"

Create()
{
	hide flare1;
	hide flare2;
	hide flare1;
	hide flare2;
	restore_delay = 3000;
	gun_1 = 0;
    spin mount around y-axis speed <25.0>;
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	start-script RockOnWaves();
}
Stop()
{
    spin mount around y-axis speed <0.0>;
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
}

ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
    turn gunbase to x-axis <-30> speed <25.00000>;
    spin mount around y-axis speed <25.0>;
}
SetStunned(State)
{
    Stunned = State;
	if (Stunned) {
	    call-script Stop();
	} else {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep 3000;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	stop-spin gunbase around y-axis;
	turn mount to y-axis heading speed <1200.000000>;
	turn gunbase to x-axis <0.000000> - pitch speed <950.000000>;
	wait-for-turn mount around y-axis;
	wait-for-turn gunbase around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if( gun_1 == 0 )
	{
	    emit-sfx 1024 + 0 from flare1;
		move guna to z-axis [-3.999994] now;
		wait-for-move guna along z-axis;
		move guna to z-axis [0.000000] speed [15.000000];
		gun_1 = 1;
	} else {
	    emit-sfx 1024 + 0 from flare2;
		move gunb to z-axis [-3.999994] now;
		wait-for-move gunb along z-axis;
		move gunb to z-axis [0.000000] speed [15.000000];
		gun_1 = 0;
	}
	
	return (1);
	
}

QueryPrimary(piecenum)
{
	if( gun_1 == 0 )
	{
		piecenum = flare2;//inverse, but needed
	}
	if( gun_1 == 1 )
	{
		piecenum = flare1;
	}
}

AimFromPrimary(piecenum)
{
	piecenum = gunbase;
}



SweetSpot(piecenum)
{
	piecenum = mount;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode mount type BITMAPONLY | NOHEATCLOUD;
		explode gunbase type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type BITMAPONLY | NOHEATCLOUD;
		explode flare2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode mount type FALL | NOHEATCLOUD;
		explode gunbase type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type FALL | NOHEATCLOUD;
		explode flare2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type SMOKE | FALL | NOHEATCLOUD;
		explode mount type SMOKE | FALL | NOHEATCLOUD;
		explode gunbase type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type SMOKE | FALL | NOHEATCLOUD;
		explode flare2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode mount type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbase type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}